#!/usr/bin/env bb

(ns script
  (:require [cheshire.core   :as json]
            [clj-yaml.core   :as yaml]
            [clojure.java.io :as io]
            [clojure.string  :as str]))

(defn item-id->file-name
  "An item ID looks like this:
     http://avi.micro.blog/2020/11/01/voted.html
   and I want the filename to be
     2020-11-01-voted.md."
  [id]
  (-> (subs id 22 (- (count id) 5))
      (str/replace "/" "-")
      (str ".md")))

(defn item->front-matter
  [item]
  (-> (select-keys item [:title])
      (assoc :date (:date_published item))
      (yaml/generate-string :dumper-options {:flow-style :block})))

(defn item->file
  [item]
  (str "---\n"
       (item->front-matter item)
       "---\n"
       (:content_text item)))

(defn write-item
  [item source-dir-path out-dir-path]
  (let [filename (item-id->file-name (:id item))
        year (subs filename 0 4)
        out-file (io/file out-dir-path year filename)]
    (io/make-parents out-file)
    (spit out-file (item->file item))))

(defn main
  [source-dir-path out-dir-path]
  (let [json-file-path (io/file source-dir-path "feed.json")
        feed (json/parse-string (slurp json-file-path) true)]
    (run! #(write-item %1 source-dir-path out-dir-path)
          (:items feed))))

(main "/Users/avi/Downloads/micro.blog export/"
      "/Users/avi/dev/aviflax.com/content/posts")
